plugins {
    id "com.github.dcendents.android-maven" version "2.1"
    id "com.jfrog.bintray" version "1.8.0"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.5.0-x"
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

def versionMajor = 0
def versionMinor = 7
def versionPatch = 4
def myVersionCode = versionMajor * 10000 + versionMinor * 100 + versionPatch
def myVersionName = "${versionMajor}.${versionMinor}.${versionPatch}"

//noinspection GroovyMissingReturnStatement
android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode myVersionCode
        versionName myVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "androidx.lifecycle:lifecycle-common:2.0.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"

    implementation "androidx.annotation:annotation:1.0.2"
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.preference:preference:1.0.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"

    implementation "com.google.code.findbugs:jsr305:3.0.2"

    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.24.5'
    testImplementation 'org.mockito:mockito-inline:2.24.5'

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$rootDir/javadoc"
    jdkVersion = 8

    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/")
    }
    externalDocumentationLink {
        url = new URL("https://developer.android.com/reference/androidx/")
    }
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(
            dir: './build/intermediates/classes/debug',
            excludes: ['eu/darken/R*.class',
                       '**/BuildConfig.class'
            ])
    sourceDirectories = files([
            'src/main/kotlin'
    ])
    executionData = files('build/jacoco/testDebugUnitTest.exec')
}

coveralls {
    jacocoReportPath = 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    sourceDirs = ['src/main/kotlin']
}

def credentialsFile = new File(System.properties['user.home'], ".bintray/bintray.properties")
ext {
    if (credentialsFile.canRead()) {
        Properties props = new Properties()
        props.load(new FileInputStream(credentialsFile))
        bintrayUser = props['bintray.user']
        bintrayApiKey = props['bintray.apikey']
    }

    bintrayRepo = 'maven'
    bintrayName = 'mvp-bakery'

    publishedGroupId = 'eu.darken.mvpbakery'
    libraryName = 'MVP Bakery'
    artifact = 'library'

    libraryDescription = 'Facilitates MVP architectures in Android'

    siteUrl = 'https://github.com/d4rken/mvp-bakery'
    gitUrl = 'https://github.com/d4rken/mvp-bakery.git'
    issuesUrl = 'https://github.com/d4rken/mvp-bakery/issues'

    libraryVersion = myVersionName

    developerId = 'darken'
    developerName = 'Matthias Urhahn'
    developerEmail = 'darken@darken.eu'

    licenseName = 'MIT'
    licenseUrl = 'https://github.com/d4rken/mvp-bakery/blob/master/LICENSE'
    allLicenses = ["MIT"]
}
if (credentialsFile.canRead()) {
    apply from: '../publish-to-bintray.gradle'
}
repositories {
    mavenCentral()
}